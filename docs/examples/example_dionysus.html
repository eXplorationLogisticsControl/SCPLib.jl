<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Dionysus problem · SCPLib.jl</title><meta name="title" content="Dionysus problem · SCPLib.jl"/><meta property="og:title" content="Dionysus problem · SCPLib.jl"/><meta property="twitter:title" content="Dionysus problem · SCPLib.jl"/><meta name="description" content="Documentation for SCPLib.jl."/><meta property="og:description" content="Documentation for SCPLib.jl."/><meta property="twitter:description" content="Documentation for SCPLib.jl."/><script data-outdated-warner src="../assets/warner.js"></script><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.050/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.4.2/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.8/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL=".."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../assets/documenter.js"></script><script src="../search_index.js"></script><script src="../siteinfo.js"></script><script src="../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/catppuccin-mocha.css" data-theme-name="catppuccin-mocha"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/catppuccin-macchiato.css" data-theme-name="catppuccin-macchiato"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/catppuccin-frappe.css" data-theme-name="catppuccin-frappe"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/catppuccin-latte.css" data-theme-name="catppuccin-latte"/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><a class="docs-logo" href="../index.html"><img src="../assets/logo.png" alt="SCPLib.jl logo"/></a><div class="docs-package-name"><span class="docs-autofit"><a href="../index.html">SCPLib.jl</a></span></div><button class="docs-search-query input is-rounded is-small is-clickable my-2 mx-auto py-1 px-2" id="documenter-search-query">Search docs (Ctrl + /)</button><ul class="docs-menu"><li><a class="tocitem" href="../index.html">Home</a></li><li><span class="tocitem">Tutorials</span><ul><li><a class="tocitem" href="../ocp_basic_cr3bp.html">Basic OCP</a></li><li><a class="tocitem" href="../ocp_ncvx.html">Non-convex path constraints</a></li></ul></li><li><span class="tocitem">Examples</span><ul><li class="is-active"><a class="tocitem" href="example_dionysus.html">Dionysus problem</a></li></ul></li><li><span class="tocitem">API</span><ul><li><a class="tocitem" href="../api_algorithms.html">API: Algorithms</a></li><li><a class="tocitem" href="../api_problems.html">API: Problems &amp; Dynamics</a></li></ul></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><a class="docs-sidebar-button docs-navbar-link fa-solid fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a><nav class="breadcrumb"><ul class="is-hidden-mobile"><li><a class="is-disabled">Examples</a></li><li class="is-active"><a href="example_dionysus.html">Dionysus problem</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href="example_dionysus.html">Dionysus problem</a></li></ul></nav><div class="docs-right"><a class="docs-navbar-link" href="https://github.com/Yuricst/SCPLib.jl" title="View the repository on GitHub"><span class="docs-icon fa-brands"></span><span class="docs-label is-hidden-touch">GitHub</span></a><a class="docs-navbar-link" href="https://github.com/Yuricst/SCPLib.jl/blob/master/docs/src/examples/example_dionysus.md" title="Edit source on GitHub"><span class="docs-icon fa-solid"></span></a><a class="docs-settings-button docs-navbar-link fa-solid fa-gear" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-article-toggle-button fa-solid fa-chevron-up" id="documenter-article-toggle-button" href="javascript:;" title="Collapse all docstrings"></a></div></header><article class="content" id="documenter-page"><h1 id="Example:-dionysus-transfer"><a class="docs-heading-anchor" href="#Example:-dionysus-transfer">Example: dionysus transfer</a><a id="Example:-dionysus-transfer-1"></a><a class="docs-heading-anchor-permalink" href="#Example:-dionysus-transfer" title="Permalink"></a></h1><pre><code class="language-julia hljs">&quot;&quot;&quot;Sample SCP problem with MEE&quot;&quot;&quot;

using Base.Threads
using Clarabel
using GLMakie
using JuMP
using LinearAlgebra
using OrdinaryDiffEq

using AstrodynamicsCore

@show nthreads()

include(joinpath(@__DIR__, &quot;../src/SCPLib.jl&quot;))

mutable struct ControlParams
    μ::Float64
    c1::Float64
    c2::Float64
    u::Vector
    function ControlParams(μ::Float64, c1::Float64, c2::Float64)
        new(μ, c1, c2, zeros(4))
    end
end

MU_SUN = 132712440018.0
G0 = 9.8065
DU = 149.6e6
VU = sqrt(MU_SUN / DU)          # velocity scale, m/s
TU = DU / VU                    # time scale, s
MASS = 4000.0                   # kg

THRUST = 0.32                    # Newtons
ISP = 3000.0                    # seconds

μ  = MU_SUN / (VU^2 * DU)
c1 = THRUST/1e3 / (MASS*DU/TU^2)               # canonical max thrust
c2 = THRUST/1e3 / (ISP*G0/1e3) / (MASS/TU)     # canonical mass flow rate
params = ControlParams(μ, c1, c2)

function eom_mee!(drvm, rvm, params, t)
    p,f,g,h,k,L,mass = rvm[1:7]   # unpack state
    cosL = cos(L)
    sinL = sin(L)
    s2 = 1 + h^2 + k^2
    w = 1 + f*cosL + g*sinL
    hsinL_kcosL = h*sinL - k*cosL
    B_mee = sqrt(p/params.μ) * [
         0    2p/w                0;
         sinL ((1+w)*cosL + f)/w -g/w*hsinL_kcosL;
        -cosL ((1+w)*sinL + g)/w  f/w*hsinL_kcosL;
         0    0                   1/w*s2/2*cosL;
         0    0                   1/w*s2/2*sinL;
         0    0                   1/w*hsinL_kcosL;
    ]
    D = [0.0, 0.0, 0.0, 0.0, 0.0, sqrt(params.μ/p^3) * (1 + f*cosL + g*sinL)^2]
    drvm[1:6] = B_mee * (params.c1 / mass) * params.u[1:3] + D
    drvm[7] = -params.u[4] * params.c2
    return
end

# -------------------- boundary conditions -------------------- #
tof = 3534 * 86400 / TU
N_rev = 5

R0 = [-3637871.081; 147099798.784; -2261.44] / DU
V0 = [-30.265097; -0.8486854; 0.0000505] / VU
RV0 = [R0; V0]
orbit_init = AstrodynamicsCore.Planet(params.μ, 0.0, RV0, &quot;initial&quot;)
mee0 = AstrodynamicsCore.rv2mee([R0; V0], params.μ)

M0 = deg2rad(114.4232)
TA0 = AstrodynamicsCore.ma2ta(M0, 0.542)
kepf = [2.2, 0.542, deg2rad(13.6), deg2rad(82.2), deg2rad(204.2), TA0]
RVf0 = AstrodynamicsCore.kep2rv(kepf, params.μ)
orbit_final = AstrodynamicsCore.Planet(params.μ, 0.0, RVf0, &quot;final&quot;)
RVf = AstrodynamicsCore.eph(orbit_final, tof + (56284 - 53400)  *86400/TU)
meef = AstrodynamicsCore.rv2mee(RVf, params.μ)
meef[6] += 2π * N_rev   # append revolutions

x0_ref = [mee0; 1.0]
xf_ref = [meef; 0.4]

# get initial and final orbits for plotting
initial_orbit_rvs = hcat([AstrodynamicsCore.eph(orbit_init, t) for t in LinRange(0.0, orbit_init.period, 100)]...)
final_orbit_rvs = hcat([AstrodynamicsCore.eph(orbit_final, t) for t in LinRange(0.0, orbit_final.period, 100)]...)

# -------------------- define objective -------------------- #
function objective(x, u)
    return -x[7,end]
end

ng = 6
function g_noncvx(x, u)
    g = AstrodynamicsCore.mee2rv(x[1:6,end], params.μ) - RVf
    return g
end

# -------------------- create problem -------------------- #
N = 500
nx = 7                              # [p,f,g,h,k,L,mass]
nu = 4                              # [ux,uy,uz,Γ]
times = LinRange(0.0, tof, N)

# create reference solution
x_ref = zeros(nx, N)
x_ref[1:6,:] = hcat(LinRange.(mee0, meef, N)...)&#39;
x_ref[7,:] = LinRange(x0_ref[7], xf_ref[7], N)
u_ref = zeros(nu, N-1)

# instantiate problem object    
prob = SCPLib.ContinuousProblem(
    Clarabel.Optimizer,
    eom_mee!,
    params,
    objective,
    times,
    x_ref,
    u_ref;
    ng = ng,
    g_noncvx = g_noncvx,
    ode_ensemble_method = EnsembleThreads(),
    ode_method = Vern7(),
)
set_silent(prob.model)

# append boundary conditions
@constraint(prob.model, constraint_initial_rv, prob.model[:x][:,1] == x0_ref)
# @constraint(prob.model, constraint_final_rv,   prob.model[:x][1:6,end] == meef)  # enforced in g_noncvx

# minimum on mass for numerical stability
@constraint(prob.model, constraint_mass_lb[k in 1:N], prob.model[:x][7,k] &gt;= 0.1)
@constraint(prob.model, constraint_p_lb[k in 1:N], prob.model[:x][1,k] &gt;= 0.8)

# append constraints on control magnitude
@constraint(prob.model, constraint_associate_control[k in 1:N-1],
    [prob.model[:u][4,k], prob.model[:u][1:3,k]...] in SecondOrderCone())
@constraint(prob.model, constraint_control_magnitude[k in 1:N-1],
    prob.model[:u][4,k] &lt;= 1.0)

sols_ig, _ = SCPLib.get_trajectory(prob, x_ref, u_ref)

# -------------------- instantiate algorithm -------------------- #
algo = SCPLib.SCvxStar(nx, N; ng=ng, w0 = 1e0, Δ0=0.1, w_max=1e20)

# solve problem
maxiter = 1000
tol_feas = 1e-6
tol_opt = 1e-6
solution = SCPLib.solve!(algo, prob, x_ref, u_ref; tol_feas = tol_feas, tol_opt = tol_opt, maxiter = maxiter)
sols_opt, g_dynamics_opt = SCPLib.get_trajectory(prob, solution.x, solution.u)
@show -solution.info[:J0][end] * MASS

# -------------------- make plot -------------------- #
fig = Figure(size=(1200,800); title=&quot;SCP problem with MEE&quot;)
ax3d = Axis3(fig[1,1:2]; aspect=:data, xlabel=&quot;x&quot;, ylabel=&quot;y&quot;, zlabel=&quot;z&quot;)
ax2d = Axis(fig[1,3]; aspect=DataAspect(), xlabel=&quot;x&quot;, ylabel=&quot;y&quot;)

scatter!(ax3d, RV0[1], RV0[2], RV0[3], color=:limegreen, markersize=10)
scatter!(ax3d, RVf[1], RVf[2], RVf[3], color=:blue, markersize=10)
lines!(ax3d, initial_orbit_rvs[1,:], initial_orbit_rvs[2,:], initial_orbit_rvs[3,:], color=:limegreen, linewidth=0.8)
lines!(ax3d, final_orbit_rvs[1,:], final_orbit_rvs[2,:], final_orbit_rvs[3,:], color=:blue, linewidth=0.8)

scatter!(ax2d, RV0[1], RV0[2], color=:limegreen, markersize=10)
scatter!(ax2d, RVf[1], RVf[2], color=:blue, markersize=10)
lines!(ax2d, initial_orbit_rvs[1,:], initial_orbit_rvs[2,:], color=:limegreen, linewidth=0.8)
lines!(ax2d, final_orbit_rvs[1,:], final_orbit_rvs[2,:], color=:blue, linewidth=0.8)

# plot initial guess
ucolor_tol = 1e-2
for (isol,sol) in enumerate(sols_ig)
    rvs = hcat([AstrodynamicsCore.mee2rv(Array(sol)[1:6,i], params.μ) for i in 1:length(sol.t)]...)
    lines!(ax3d, rvs[1,:], rvs[2,:], rvs[3,:], color=:grey, linewidth=1.0)
    lines!(ax2d, rvs[1,:], rvs[2,:], color=:grey, linewidth=1.0)
end

# plot optimal solution
ucolor_tol = 1e-2
for (isol,sol) in enumerate(sols_opt)
    rvs = hcat([AstrodynamicsCore.mee2rv(Array(sol)[1:6,i], params.μ) for i in 1:length(sol.t)]...)
    lines!(ax3d, rvs[1,:], rvs[2,:], rvs[3,:], color=u_ref[4,isol] &gt; ucolor_tol ? :red : :black, linewidth=1.0)
    lines!(ax2d, rvs[1,:], rvs[2,:], color=u_ref[4,isol] &gt; ucolor_tol ? :red : :black, linewidth=1.0)
end

axm = Axis(fig[2,1]; xlabel=&quot;Time, day&quot;, ylabel=&quot;Mass&quot;, xticks=0:500:3500)
for (isol,sol) in enumerate(sols_opt)
    lines!(axm, sol.t*TU/86400, Array(sol)[7,:] * MASS, color=:black, linewidth=1.0)
end
hlines!(axm, solution.x[7,end]*MASS, color=:black, linewidth=1.0, label=&quot;mf = $(round(solution.x[7,end] * MASS * 1e4)/1e4) kg&quot;, linestyle=:dash)
axislegend(axm, position=:cc)

axu = Axis(fig[2,2]; xlabel=&quot;Time, day&quot;, ylabel=&quot;Control&quot;, xticks=0:500:3500)
stairs!(axu, times*TU/86400, [solution.u[1,:]; 0.0]; step=:post, color = :blue, linewidth=0.8)
stairs!(axu, times*TU/86400, [solution.u[2,:]; 0.0]; step=:post, color = :red, linewidth=0.8)
stairs!(axu, times*TU/86400, [solution.u[3,:]; 0.0]; step=:post, color = :limegreen, linewidth=0.8)
stairs!(axu, times*TU/86400, [solution.u[4,:]; 0.0]; step=:post, color = :black, linewidth=0.8, linestyle=:dash)

display(fig)</code></pre><p><img src="../assets/advanced_dionysus.png" alt="SCvxStar quadcoptor trajectory solution"/></p></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../ocp_ncvx.html">« Non-convex path constraints</a><a class="docs-footer-nextpage" href="../api_algorithms.html">API: Algorithms »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="auto">Automatic (OS)</option><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option><option value="catppuccin-latte">catppuccin-latte</option><option value="catppuccin-frappe">catppuccin-frappe</option><option value="catppuccin-macchiato">catppuccin-macchiato</option><option value="catppuccin-mocha">catppuccin-mocha</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 1.16.1 on <span class="colophon-date" title="Saturday 14 February 2026 23:36">Saturday 14 February 2026</span>. Using Julia version 1.10.0.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
